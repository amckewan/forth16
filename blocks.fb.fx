S 0
EBkQGgkgAP8ACP9PSwoAAAAAAAAAAAAAAAAAAAAAAAAHMAAHBAAHAQAICwAAAAAAU1VCCgAAAAAAAAAAAAAAAA==
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA==
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA==
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA==
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA==
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA==
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA==
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA==
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA==
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA==
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA==
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA==
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA==
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA==
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA==
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA==
B 1
S 9
( Electives)   FORTH DEFINITIONS DECIMAL
( Utils)  10 LOAD  11 LOAD
( Editor)  30 LOAD

: HI ." ready" ;
GILD










S 10
( Screen index )
: USED? ( scr -- f )   0 SWAP BLOCK C/L BOUNDS DO
   I C@ BL < IF DROP 0 LEAVE THEN  I C@ BL > +  LOOP ;
: INDEX ( start end -- )  1+ SWAP DO
   CR I 4 .R SPACE  I USED? IF I BLOCK C/L TYPE THEN  LOOP ;
: QX ( n)   60 / 60 * DUP 60 + SWAP DO
   I 3 MOD 0= IF CR THEN  I 4 .R SPACE
   I USED? IF I BLOCK 19 TYPE ELSE 19 SPACES THEN  SPACE LOOP ;

1024 CONSTANT B/BUF
: COPY ( from to -)  SWAP BLOCK SWAP BUFFER B/BUF MOVE UPDATE ;





S 11
( String operators from F83)
( Delete count chars at the start of the buffer, blank to end)
: DELETE   ( buffer size count -- )
   OVER MIN >R  R@ - ( left over )  DUP 0>
   IF  2DUP SWAP DUP R@ + -ROT SWAP CMOVE  THEN  + R> BLANK ;

( Insert a string into the start of the buffer, end chars lost)
: INSERT   ( string length buffer size -- )
   ROT OVER MIN >R  R@ - ( left over )
   OVER DUP R@ +  ROT CMOVE>   R> CMOVE  ;

( Overwrite string at the start of buffer)
: REPLACE ( string length buffer size -- )  ROT MIN CMOVE ;



B 12
S 15
( Cross compiler)  EMPTY
( Following polyFORTH manual)   HEX
0017 VOCABULARY HOST  IMMEDIATE  HOST DEFINITIONS
0179 VOCABULARY ASSEMBLER
000B VOCABULARY TARGET ( Target's FORTH )

0071 VOCABULARY FORTH  IMMEDIATE  DECIMAL

( Memory) 16 LOAD
( Save) 17 LOAD
( Heads) 18 LOAD





S 16
( Target memory )
CREATE IMAGE ( 8K )  HERE 8192  DUP ALLOT ERASE
: TC@ ( ta -- u8 )   IMAGE + C@ ;
: TC! ( u8 ta -- )   IMAGE + C! ;
: T@  ( ta -- u16 )  DUP TC@  SWAP 1+ TC@  8 LSHIFT OR ;
: T!  ( u16 ta -- )  2DUP TC!  SWAP 8 RSHIFT SWAP  1+ TC! ;

VARIABLE H
: HERE  ( -- taddr )   H @ ;
: ALLOT ( n -- )       H +! ;
: C,    ( char -- )    HERE TC!   1 H +! ;
: ,     ( n -- )       HERE  T!   2 H +! ;
: S, ( addr len -- )   0 ?DO  COUNT C,  LOOP DROP ;

: ALIGN ( -- )   HERE 1 AND IF 0 C, THEN ;

S 17
( Save image)
( Just 1 block for now)
: SAVE   IMAGE 0 BUFFER B/BUF CMOVE UPDATE ;













S 18
( Create target words)
CREATE CONTEXT   FORTH 1 , HERE 8 CELLS DUP ALLOT ERASE HOST
VARIABLE CURRENT   1 CURRENT !
VARIABLE WIDTH     3 DUP WIDTH C! WIDTH 1+ C!
VARIABLE LAST ( nfa)
: HASH ( str voc - 'link)
   2/  SWAP 1+ C@ +  7 AND  1+ CELLS CONTEXT + ;
: NAME ( str -)   HERE LAST !  COUNT DUP $80 OR C,
   WIDTH C@ MIN  1 ?DO COUNT C, LOOP  C@ $80 OR C,
   WIDTH 1+ C@ WIDTH C! ;
: TOGGLE ( n)   LAST @  DUP TC@ ROT XOR  SWAP TC! ;
: SMUDGE      $20 TOGGLE ;
: IMMEDIATE   $40 TOGGLE ;
: HEADER   ALIGN HERE  BL WORD  DUP CURRENT @ HASH
   DUP @ ,  ROT SWAP !  NAME ;

B 19
S 30
( Editor )   DECIMAL

: F83-SEARCH   ( sadr slen badr blen -- n f )
   DUP >R  2SWAP SEARCH DUP IF  R@ ROT - SWAP  THEN
   ROT R> 2DROP ;

EDITOR DEFINITIONS   31 38 THRU   FORTH DEFINITIONS

: LIST ( n)   [ EDITOR ] TOP LIST EDITOR ; FORTH







S 31
( Move the Editor's cursor around)
VARIABLE R# ( cursor, 0-1023)
: TOP          ( -- )      0 R# ! ;
: C            ( n -- )    R# @ + B/BUF MOD R# ! ;
: T            ( n -- )    TOP  C/L *  C ;
: CURSOR       ( -- n )    R# @ ;
: LINE#        ( -- n )    CURSOR  C/L  /  ;
: COL#         ( -- n )    CURSOR  C/L  MOD  ;
: 'START       ( -- adr )  SCR @ BLOCK ;
: 'CURSOR      ( -- adr )  'START  CURSOR  + ;
: 'LINE        ( -- adr )  'CURSOR  COL# -  ;
: #AFTER       ( -- n )    C/L COL# -  ;
: #REMAINING   ( -- n )    B/BUF CURSOR - ;
: #END         ( -- n )    #REMAINING COL# +  ;


S 32
( Editor buffers)
VARIABLE CHANGED
: MODIFIED   ( -- )   CHANGED ON  UPDATE ;
: ?TEXT   ( adr -- adr+1 n )   >R  94 PARSE DUP
   IF  R@ C/L 1+ BLANK  R@ PLACE  ELSE  2DROP  THEN  R> COUNT ;
84 CONSTANT C/PAD
: 'INSERT   ( -- insert-buffer )   PAD     C/PAD + ;
: 'FIND     ( -- find-buffer )     'INSERT C/PAD + ;
: .FRAMED   ( adr -- )   ." '" COUNT TYPE ." '" ;
: .BUFS     ( -- )
   CR ." I " 'INSERT .FRAMED   CR ." F " 'FIND .FRAMED ;
: ?MISSING   ( n f -- n | )
   0= IF  DROP 'FIND .FRAMED ."  not found " QUIT THEN ;
: KEEP   ( -- )   'LINE C/L 'INSERT  PLACE  ;


S 33
( Editor buffers)
: K   ( -- )   'FIND PAD  C/PAD CMOVE
   'INSERT 'FIND  C/PAD CMOVE   PAD 'INSERT  C/PAD CMOVE  ;
: W   ( -- )   SAVE-BUFFERS  ;
: 'C#A   ( -- 'cursor #after )   'CURSOR #AFTER  MODIFIED  ;
: (I)  ( -- len 'insert len 'cursor #after )
   'INSERT ?TEXT  TUCK 'C#A  ;
: (TILL)  ( -- n )   'FIND ?TEXT 'C#A F83-SEARCH ?MISSING ;
: 'F+   ( n1 -- n2 )  'FIND C@ +  ;







S 34
( Line editing)
: I   ( -- )   (I)  INSERT  C ;
: O   ( -- )   (I)  REPLACE C ;
: P   ( -- )   'INSERT ?TEXT DROP 'LINE C/L CMOVE MODIFIED ;
: U   ( -- )   C/L C 'LINE C/L OVER #END INSERT  P ;
: X   ( -- )   KEEP  'LINE #END C/L  DELETE MODIFIED ;
: SPLIT  ( -- ) \ breaks the current line in two at the cursor
   PAD C/L 2DUP BLANK 'CURSOR #REMAINING INSERT MODIFIED ;
: JOIN   ( -- )   'LINE C/L + C/L  'C#A  INSERT ;
: WIPE   ( -- )   'START B/BUF BLANK  MODIFIED ;
: G   (  screen line -- )  ( not M? )
   C/L * SWAP BLOCK +  C/L 'INSERT PLACE
   C/L NEGATE C  U  C/L C ;
: BRING   ( screen first last -- )
   1+ SWAP DO  DUP [ FORTH ] I [ EDITOR ] G  LOOP  DROP ;

S 35
( Find and replace)
: FIND? ( - n f ) 'FIND ?TEXT  'CURSOR #REMAINING  F83-SEARCH ;
: F   ( -- )   FIND? ?MISSING   'F+ C ;
: ?ENOUGH ( n)   1+ DEPTH > ABORT" arg?" ;
: S   ( n - )   1 ?ENOUGH   FIND?
   IF  'F+ C  EXIT  THEN  DROP  FALSE OVER SCR @
   DO   1 SCR +! TOP  'FIND COUNT 'CURSOR #REMAINING F83-SEARCH
     IF  'F+ C DROP TRUE LEAVE  ELSE  DROP  THEN
     KEY? ABORT" Break!"
   LOOP  ?MISSING ;
: E   ( -- )   'FIND C@  DUP NEGATE C  'C#A ROT DELETE ;
: D   ( -- )   F E ;
: R   ( -- )   E I ;
: TILL    ( -- )   'C#A (TILL)  'F+  DELETE ;
: J       ( -- )   'C#A (TILL)  DELETE ;
: KT      ( -- )   'CURSOR (TILL)  'F+  'INSERT PLACE  ;
S 36
( Editor new lines )
: NEW ( n)   16 SWAP
   DO [ FORTH ] I [ EDITOR ] DUP T  CR 2 .R SPACE  QUERY
      #TIB @ IF  P  ELSE  LEAVE  THEN
   LOOP ;











S 37
( Editor LIST)
: .LN ( n)   2 .R SPACE ;
: .ROW ( row)  DUP .LN  C/L * 'START +  C/L TYPE ;
: .LINE   LINE# .LN
   'LINE COL# TYPE  94 EMIT  'CURSOR #AFTER TYPE ;

: LIST ( n)   DUP SCR !  ." Scr " .
   16 0 DO  [ FORTH ] I [ EDITOR ]  CR
      DUP LINE# = IF DROP .LINE ELSE .ROW THEN ." |"
   LOOP ;

: PLAIN ( n)   BLOCK  16 0 DO
   DUP C/L -TRAILING CR TYPE  C/L +   LOOP DROP  CR ;



S 38
( Editor UI)
( Display current line if there are no more commands)
: ?L   >IN @ #TIB @ = IF  CR .LINE  THEN ;

: T ( n - )   DEPTH IF T THEN ?L ;

: F F ?L ;   : S S ?L ;   : E E ?L ;   : D D ?L ;
: O O ?L ;   : R R ?L ;   : I I ?L ;   : J J ?L ;
: TILL TILL ?L ;

: L  SCR @ LIST ;   : N  1 SCR +! L ;   : B  -1 SCR +! L ;
: LL  TOP L ;




B 39
S 45
( Assembler )
FORTH DEFINITIONS

( mem access, define as needed)
: AHERE HERE ;
: AC@ C@ ;   : AC! C! ;
: A@ @ ;     : A! ! ;









S 46
( Assembler)
ASSEMBLER DEFINITIONS HEX
0 CONSTANT T   1 CONSTANT S   2 CONSTANT R   3 CONSTANT P
4 CONSTANT I   5 CONSTANT W
: OP, ( n op -)   OR TC, ;
: LD ( r)   ?DUP IF  10 OP,  ELSE  10 TC, T,  THEN ;
: ST ( r)   DUP 0= ABORT" T?"  18 OP, ;









B 47
Z 51
B 59
E 60
